data {
  int<lower=1> N;
  int<lower=2> T;
  array[N, T] int<lower=0,upper=1> choice;
  array[N, T] int<lower=0,upper=1> win;
}

parameters {
  real<lower=0,upper=1> p_repeat_win;
  real<lower=0,upper=1> p_repeat_loss;
  real<lower=0,upper=0.3> lapse;
}

model {
  // Priors
  p_repeat_win  ~ beta(2, 2);
  p_repeat_loss ~ beta(2, 2);
  lapse         ~ beta(1, 12);

  // Likelihood
  for (n in 1:N) {
    for (t in 2:T) {
      int is_repeat;
      real base_p;
      real p;

      is_repeat = (choice[n, t] == choice[n, t - 1]);
      base_p = (win[n, t - 1] == 1) ? p_repeat_win : p_repeat_loss;
      p = (1 - lapse) * base_p + lapse * 0.5;

      is_repeat ~ bernoulli(p);
    }
  }
}

generated quantities {
  real p_eff_win  = (1 - lapse) * p_repeat_win  + lapse * 0.5;
  real p_eff_loss = (1 - lapse) * p_repeat_loss + lapse * 0.5;
}
